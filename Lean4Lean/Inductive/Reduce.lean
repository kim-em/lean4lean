import Lean.Structure
import Lean4Lean.Expr
import Lean4Lean.Environment.Basic

namespace Lean4Lean
open Lean hiding Environment
open Kernel

section
variable [Monad m] (env : Environment)
    (whnf : Expr → m Expr) (inferType : Expr → m Expr) (isDefEq : Expr → Expr → m Bool)

def getFirstCtor (dName : Name) : Option Name := do
  let some (.inductInfo info) := env.find? dName | none
  info.ctors.head?

def mkNullaryCtor (type : Expr) (nparams : Nat) : Option Expr :=
  type.withApp fun d args => do
  let .const dName ls := d | none
  let name ← getFirstCtor env dName
  return mkAppRange (.const name ls) 0 nparams args

/-- When `e` has the type of a K-like inductive, converts it into a constructor application.

For instance if we have `e : Eq a a`, it is converted into `Eq.refl a` (which it is definitionally
equal to by proof irrelevance). Note that the indices of `e`'s type must match those of the
constructor application (for instance, `e : Eq a b` cannot be converted if `a` and `b` are not
defeq). -/
def toCtorWhenK (rval : RecursorVal) (e : Expr) : m Expr := do
  assert! rval.k
  let appType ← whnf (← inferType e)
  let .const appTypeI _ := appType.getAppFn | return e
  if appTypeI != rval.getMajorInduct then return e
  if appType.hasExprMVar && appType.getAppArgs.any (·.hasExprMVar) rval.numParams then return e
  let some newCtorApp := mkNullaryCtor env appType rval.numParams | return e
  -- check that the indices of types of `e` and `newCtorApp` match
  unless ← isDefEq appType (← inferType newCtorApp) do return e
  return newCtorApp

def expandEtaStruct (eType e : Expr) : Expr :=
  eType.withApp fun I args => Id.run do
  let .const I ls := I | return e
  let some (.inductInfo sInfo) := env.find? I | return e
  let some ctor := sInfo.ctors.head? | return e
  let some (.ctorInfo info) := env.find? ctor | return e
  if info.induct != I then return e
  let result := mkAppRange (.const ctor ls) 0 info.numParams args
  pure <| (List.range info.numFields).foldl (fun result i => .app result (.proj I i e)) result

/-- When `e` is of non-recursive structure type, and that type is not a proposition, converts `e`
into a constructor application using projections.

For instance if we have `e : α × β`, it is converted into `Prod.mk α β e.1 e.2` (which is
definitionally equal to `e` by struct eta). -/
def toCtorWhenStruct (inductName : Name) (e : Expr) : m Expr := do
  if !env.isNonRecStructure inductName || (e.isConstructorApp?' env).isSome then
    return e
  let eType ← whnf (← inferType e)
  if !eType.getAppFn.isConstOf inductName then return e
  let .sort u ← whnf (← inferType eType) | return e
  unless u.isNeverZero do return e
  return expandEtaStruct env eType e

def getRecRuleFor (rval : RecursorVal) (major : Expr) : Option RecursorRule := do
  let .const fn _ := major.getAppFn | none
  rval.rules.find? (·.ctor == fn)

/-- The final phase of recursor reduction: apply the rule for the constructor at the head of the
(converted) major premise to the parameters, motives, and minors, the constructor's fields, and
the remaining arguments. -/
def inductiveReduceRecTail (info : RecursorVal) (ls : List Level) (recArgs : Array Expr)
    (major : Expr) : Option Expr := do
  let some rule := getRecRuleFor info major | none
  let majorArgs := major.getAppArgs
  -- Restored nested recursors need not bind the constructor's parameters: for
  -- example, `Lean.Syntax.rec_1` eliminates `Array Lean.Syntax` with no parameters.
  -- Constructor fields are the suffix, independently of the recursor telescope.
  if rule.nfields > majorArgs.size then none
  if ls.length != info.levelParams.length then none
  let mut rhs := rule.rhs.instantiateLevelParams info.levelParams ls
  -- get the parameters, motives and minor premises from the recursor application (recursor rules
  -- don't need the indices, as these are determined by the constructor and its parameters/fields)
  rhs := mkAppRange rhs 0 info.getFirstIndexIdx recArgs
  -- get fields from constructor application
  rhs := mkAppRange rhs (majorArgs.size - rule.nfields) majorArgs.size majorArgs
  let majorIdx := info.getMajorIdx
  if majorIdx + 1 < recArgs.size then
    rhs := mkAppRange rhs (majorIdx + 1) recArgs.size recArgs
  return rhs

/-- Performs recursor reduction on `e` (returning `none` if not applicable).

For recursor reduction to occur, `e` must be a recursor application where the major premise is
either a complete constructor application, a `Nat` or `String` literal, or of a K- or
structure-like inductive type (in each case it is converted into an equivalent constructor
application). The reduction is done by applying the `RecursorRule.rhs` associated with the
constructor to everything before the indices in the recursor application (its parameters, motives
and minor premises) and then to the fields of the constructor application; any arguments after the
major premise are re-applied to the result. -/
def inductiveReduceRec [Monad m] (env : Environment) (e : Expr)
    (whnf : Expr → m Expr) (inferType : Expr → m Expr) (isDefEq : Expr → Expr → m Bool) :
    m (Option Expr) := do
  let .const recFn ls := e.getAppFn | return none
  let some (.recInfo info) := env.find? recFn | return none
  let recArgs := e.getAppArgs
  let majorIdx := info.getMajorIdx
  let some major := recArgs[majorIdx]? | return none
  let mut major := major
  if info.k then
    major ← toCtorWhenK env whnf inferType isDefEq info major
  match ← whnf major with
  | .lit (.natVal n) => major := .natLitToConstructor n
  | .lit (.strVal s) => major ← whnf (.strLitToConstructor s)
  | e => major ← toCtorWhenStruct env whnf inferType info.getMajorInduct e
  return inductiveReduceRecTail info ls recArgs major

end
